variable "environment" {
  description = "Name of the AWS deployment environment"
  type        = string
}

variable "namespace" {
  description = "Prefix for all resources generated by the pipeline"
  type        = string
}

variable "serverless_role_arn" {
  description = "Execution role of the lambda function"
  type        = string
}

variable "vpc_id" {
  description = "VPC ID passed to resources, resolved via SSM"
  type        = string
}

variable "island_subnet_a" {
  description = "Subnets used for Service Template, passed via foundational.json"
  type        = string
}

variable "island_subnet_b" {
  description = "Subnets used for Service Template, passed via foundational.json"
  type        = string
}

variable "routable_subnet_a" {
  description = "Subnets used for Service Template, passed via foundational.json"
  type        = string
}

variable "routable_subnet_b" {
  description = "Subnets used for Service Template, passed via foundational.json"
  type        = string
}

variable "ecs_cluster" {
  description = "Cluster used to create and execute tasks"
  type        = string
}

variable "log_group" {
  description = "Log group for task logging"
  type        = string
}

variable "ecs_task_role_arn" {
  description = "ARN for role to create tasks in ECS"
  type        = string
}

variable "ecs_task_execution_role_arn" {
  description = "ARN for role to execute tasks in ECS"
  type        = string
}

variable "image" {
  description = "The container to run tasks in"
  type        = string
}

variable "github_secret_arn" {
  description = "Secret ARN of PAT for github actions"
  type        = string
}

variable "tag" {
  description = "Used as the aws log stream prefix"
  type        = string
}

variable "container_security_group" {
  description = "Security group to apply to containers"
  type        = string
}

variable "serverless_security_group" {
  description = "Security group to apply to lambda"
  type        = string
}

variable "vpce_security_group" {
  description = "Security group to apply to vpc endpoint"
  type        = string
}

variable "git_hook_secret" {
  description = "Secret for GithubHook"
  type        = string
}

variable "runner_group" {
  description = "Name of the runner group"
  type        = string
}

variable "runner_labels" {
  description = "Comma-separated list of runner labels"
  type        = string
}

resource "aws_ecs_task_definition" "runner_task_definition" {
  family                   = var.namespace
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = "2048"
  memory                   = "4096"
  task_role_arn            = var.ecs_task_role_arn
  execution_role_arn       = var.ecs_task_execution_role_arn

  container_definitions = jsonencode([
    {
      name      = var.namespace
      image     = var.image
      essential = true
      cpu       = 2048
      memory    = 4096
      environment = [
        {
          name  = "NAMESPACE"
          value = var.namespace
        },
        {
          name  = "RUNNER_GROUP"
          value = var.runner_group
        },
        {
          name  = "RUNNER_LABELS"
          value = var.runner_labels
        }
      ]
      command = ["./entrypoint_token.sh"]
      log_configuration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = var.log_group
          awslogs-region        = "us-west-2"
          awslogs-stream-prefix = var.tag
        }
      }
    }
  ])
}

resource "aws_vpc_endpoint" "vpc_endpoint" {
  vpc_id             = var.vpc_id
  service_name       = "com.amazonaws.${data.aws_region.current.name}.execute-api"
  vpc_endpoint_type  = "Interface"
  security_group_ids = [var.vpce_security_group]
  subnet_ids         = [var.routable_subnet_a, var.routable_subnet_b]
}

resource "aws_apigatewayv2_api" "hook_api" {
  name          = "${var.namespace}-github-workflow_job hook"
  protocol_type = "HTTP"

  target = aws_lambda_function.function.arn
}

resource "aws_lambda_function" "function" {
  function_name = var.namespace
  handler       = "handler.handler"
  runtime       = "python3.9"
  role          = var.serverless_role_arn
  filename      = "../runnerhook.zip"
  memory_size   = 128
  timeout       = 30

  environment {
    variables = {
      AWS_ENV                  = var.environment
      GITHUB_SECRET_ARN        = var.github_secret_arn
      GITHUB_API_URL           = "https://github.mmm.com/api/v3"
      GITHUB_HOOK_SECRET       = var.git_hook_secret
      ECS_CLUSTER              = var.ecs_cluster
      TASK_DEFINITION_ARN      = aws_ecs_task_definition.runner_task_definition.arn
      CONTAINER_NAME           = var.namespace
      CONTAINER_SECURITY_GROUP = var.container_security_group
      SUBNET_A                 = var.island_subnet_a
      SUBNET_B                 = var.island_subnet_b
      RUNNER_LABELS            = var.runner_labels
    }
  }

  vpc_config {
    security_group_ids = [var.serverless_security_group]
    subnet_ids         = [var.island_subnet_a, var.island_subnet_b]
  }
}

resource "aws_ssm_parameter" "hook_url_ssm_parameter" {
  name        = "${var.namespace}HookUrl"
  description = "The API endpoint for runner github webhook"
  type        = "String"
  value       = "https://${aws_apigatewayv2_api.hook_api.id}.execute-api.${data.aws_region.current.name}.amazonaws.com/prod/workflow/run"
}

output "function_name" {
  value = aws_lambda_function.function.function_name
}

output "web_hook_url" {
  value = aws_ssm_parameter.hook_url_ssm_parameter.value
}
